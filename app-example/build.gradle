configureAndroidApp(project)

def getAccessToken() {
    def systemEnvKey = System.getenv('ACCESS_KEY')

    def props = new Properties()
    def propsFile = file("local.properties")
    if (propsFile.exists()) {
        propsFile.withInputStream { props.load(it) }
    }
    def localPropsKey = props.getProperty("accessKey")

    return localPropsKey ?: systemEnvKey ?: "\"\""
}

def getClientSecret() {
    def systemEnvKey = System.getenv('CLIENT_SECRET')

    def props = new Properties()
    def propsFile = file("local.properties")
    if (propsFile.exists()) {
        propsFile.withInputStream { props.load(it) }
    }
    def localPropsKey = props.getProperty("clientSecret")

    return localPropsKey ?: systemEnvKey ?: "\"\""
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.badoo.ribs.example"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "ACCESS_KEY", getAccessToken()
        buildConfigField "String", "CLIENT_SECRET", getClientSecret()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=com.badoo.ribs.annotation.ExperimentalApi"
    }
}

dependencies {
    implementation deps.android.appCompat
    implementation deps.android.design
    implementation deps.android.constraintLayout
    implementation deps.android.recyclerView
    implementation deps.external.coil

    implementation deps.external.rxrelay2
    implementation deps.external.rxandroid2

    implementation deps.retrofit.retrofit
    implementation deps.retrofit.rxjavaAdapter
    implementation deps.retrofit.moshiConverter
    implementation deps.retrofit.okhttp
    implementation deps.retrofit.okhttpLoggingInterceptor

    implementation project(":libraries:rib-base")
    implementation project(':libraries:rib-rx2')
    implementation project(":libraries:rib-portal")

    implementation deps.external.mviCore
    implementation deps.external.mviCoreAndroid
    implementation deps.external.mviCoreDiff
    implementation project(path: ':libraries:rib-debug-utils')

    junitTestImplementation(project)
    testImplementation deps.test.mockitoKotlin
    testImplementation deps.test.assertj
    testImplementation project(":libraries:rib-base-test")
    testImplementation project(":libraries:rib-base-test-rx2")

    junitAndroidTestImplementation(project)
    androidTestImplementation project(":libraries:rib-base-test")
    androidTestImplementation deps.androidTest.espresso
    androidTestImplementation deps.androidTest.runner

    debugImplementation project(":libraries:rib-base-test-activity")
}
